/*
 * nto.link
 *	Loader command script for Neutrino
 *
 */
ENTRY(_start)
PHDRS {
	text	PT_LOAD;
	data	PT_LOAD;
	note	PT_NOTE;
}

SECTIONS {
	.text 0x8020000 : {
		*(.text*)
		etext = .;
		_etext = .;
		*(.reginfo)
	}:text
	.rodata ALIGN(0x1000) : {
		_fdata = .;
		*(.rodata_mod_start)
		module_list_start = .;	
		*(.rodata_mod)
		module_list_end = .;	
		*(.rodata_mod_end)
		*(.rodata*)
	}:data
	.data . : {
		_data = .;
		*(.data)
		*(.eh_frame*)
	}
	.ctors . : {
		*(.ctors)
	}
	.lit8 . : {
		*(.lit8)
	}
	.lit4 . : {
		*(.lit4)
	}
	.sdata . : {
		_gp = . ;
		_sdata = .;
		*(.sdata)
		edata = .;
		_edata = .;
	}
	.sbss . : {
		__bss_start = .;
		_fbss = .;
		*(.sbss) *(.scommon)
	}
	.bss . : {
		*(.bss)
		*(COMMON)
		_end = .;
		end = .;
	}
	.debug 0x0 : { *(.debug) }:NONE
	.debug_srcinfo 0x0 : { *(.debug_srcinfo) }:NONE
	.debug_aranges 0x0 : { *(.debug_aranges) }:NONE
	.debug_pubnames 0x0 : { *(.debug_pubnames) }:NONE
	.debug_sfnames 0x0 : { *(.debug_sfnames) }:NONE
	.line 0x0 : { *(.line) }:NONE
}
